plugins {
    id 'nu.studer.jooq' version '3.0.3'
    id "org.flywaydb.flyway" version "5.2.4"
}

configurations {
    flywayMigration
}

dependencies {
    api project(":public-persistence-api")
    api project(":common-persistence-jooq")
    implementation project(":public-entity")
    implementation project(":common-utils")

    jooqRuntime "org.postgresql:postgresql"
    runtimeOnly "org.postgresql:postgresql"
    api "org.jooq:jooq"

    implementation("org.apache.commons:commons-lang3")
    implementation("org.apache.commons:commons-collections4:4.3")

    testLibCompile project(":common-persistence-jooq-test")
    testLibCompile project(":common-test")

    testLibCompile "org.projectlombok:lombok"
    testLibAnnotationProcessor "org.projectlombok:lombok"

    flywayMigration "org.postgresql:postgresql"
    integrationTestRuntimeOnly "org.postgresql:postgresql"

    integrationTestCompile "org.projectlombok:lombok"
    integrationTestAnnotationProcessor "org.projectlombok:lombok"
}

description = "SciPaMaTo-Public:: Persistence jOOQ Project"

def generatedSourcesPath = 'build/generated-src/jooq'
sourceSets.main.java.srcDirs = [generatedSourcesPath, 'src/main/java']

def bootPubProps = new Properties()
file("src/main/resources/application.properties").withInputStream {
    bootPubProps.load(it)
}

def bootPubItProps = new Properties()
file("src/intTest/resources/application.properties").withInputStream {
    bootPubItProps.load(it)
}


jooq {
    edition = 'OSS'
    scipamatoPublic(sourceSets.main) {
        jdbc {
            driver = bootPubProps['spring.datasource.driver-class-name']
            url = bootPubProps['spring.datasource.url']
            user = bootPubProps['spring.datasource.hikari.username']
            password = bootPubProps['spring.datasource.hikari.password']
        }
        generator {
            name = 'org.jooq.codegen.DefaultGenerator'
            strategy {
                name = 'org.jooq.codegen.DefaultGeneratorStrategy'
            }
            database {
                name = 'org.jooq.meta.postgres.PostgresDatabase'
                inputSchema = bootPubProps['db.schema']
                recordVersionFields = 'version'
            }
            generate {
                deprecated = false
                instanceFields = true
                pojos = false
                springAnnotations = true
            }
            target {
                packageName = 'ch.difty.scipamato.publ.db'
                directory = generatedSourcesPath
            }
        }
    }
    scipamatoPublicIt(sourceSets.integrationTest) {
        jdbc {
            driver = bootPubItProps['spring.datasource.driver-class-name']
            url = bootPubItProps['spring.datasource.url']
            user = bootPubItProps['spring.datasource.hikari.username']
            password = bootPubItProps['spring.datasource.hikari.password']
        }
        generator {
            name = 'org.jooq.codegen.DefaultGenerator'
            strategy {
                name = 'org.jooq.codegen.DefaultGeneratorStrategy'
            }
            database {
                name = 'org.jooq.meta.postgres.PostgresDatabase'
                inputSchema = bootPubItProps['db.schema']
                recordVersionFields = 'version'
            }
            generate {
                deprecated = false
                instanceFields = true
                pojos = false
                springAnnotations = true
            }
            target {
                packageName = 'ch.difty.scipamato.publ.db'
                directory = generatedSourcesPath
            }
        }
    }
}

flyway {
    url = bootPubProps['spring.datasource.url']
    user = bootPubProps['spring.flyway.user']
    password = bootPubProps['spring.flyway.password']
    schemas = [bootPubProps['db.schema']]
    configurations = ['compile', 'flywayMigration']
}

task flywayMigrateIt(type: org.flywaydb.gradle.task.FlywayMigrateTask) {
    description = 'Migrates the schema of the integration test database'
    url = bootPubItProps['spring.datasource.url']
    user = bootPubItProps['spring.flyway.user']
    password = bootPubItProps['spring.flyway.password']
    schemas = [bootPubItProps['db.schema']]
    locations = [bootPubItProps['spring.flyway.locations']]
    configurations = ['compile', 'flywayMigration']
}

task flywayCleanIt(type: org.flywaydb.gradle.task.FlywayCleanTask) {
    description = 'Drops all objects in the configured schemas of the integration test database'
    url = bootPubItProps['spring.datasource.url']
    user = bootPubItProps['spring.flyway.user']
    password = bootPubItProps['spring.flyway.password']
    schemas = [bootPubItProps['db.schema']]
    configurations = ['compile', 'flywayMigration']
}

task flywayInfoIt(type: org.flywaydb.gradle.task.FlywayInfoTask) {
    description = 'Prints the details and status information about all the migrations.'
    url = bootPubItProps['spring.datasource.url']
    user = bootPubItProps['spring.flyway.user']
    password = bootPubItProps['spring.flyway.password']
    schemas = [bootPubItProps['db.schema']]
    configurations = ['compile', 'flywayMigration']
}

compileKotlin.dependsOn generateScipamatoPublicJooqSchemaSource
generateScipamatoPublicJooqSchemaSource.dependsOn flywayMigrate
generateScipamatoPublicItJooqSchemaSource.dependsOn flywayMigrateIt


project.tasks.getByName('compileJava').dependsOn -= 'generateScipamatoPublicItJooqSchemaSource'
project.tasks.getByName('integrationTest').dependsOn += 'generateScipamatoPublicItJooqSchemaSource'